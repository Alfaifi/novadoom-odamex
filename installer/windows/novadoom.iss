; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; Must be passed as a /D parameter to the compiler
;#define NovaDoomVersion "0.0.1"
;#define NovaDoomTestSuffix "-TEST1"
;#define SourcePath "C:\novadoom"

#define NovaDoomName "NovaDoom"
#define NovaDoomPublisher "NovaDoom Team"
#define NovaDoomURL "https://novadoom.com/"

[Setup]
AppName={#NovaDoomName}
AppVersion={#NovaDoomVersion}
AppVerName={#NovaDoomName + " " + NovaDoomVersion + NovaDoomTestSuffix}
AppPublisher={#NovaDoomPublisher}
AppPublisherURL={#NovaDoomURL}
AppSupportURL={#NovaDoomURL}
AppUpdatesURL={#NovaDoomURL}
VersionInfoCompany={#NovaDoomName}
VersionInfoProductName={#NovaDoomName} Installer
VersionInfoProductVersion={#NovaDoomVersion}
VersionInfoVersion={#NovaDoomVersion}
DefaultDirName={autopf}\{#NovaDoomName}
DefaultGroupName={#NovaDoomName}
AllowNoIcons=true
LicenseFile={#SourcePath}\LICENSE
OutputBaseFilename={#"novadoom-win-" + NovaDoomVersion + NovaDoomTestSuffix}
Compression=lzma2
SolidCompression=true
AlwaysShowDirOnReadyPage=true
ChangesEnvironment=true
AppID={{2E517BBB-916F-4AB6-80E0-D4A292513F7A}
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
ShowLanguageDialog=auto
UninstallDisplayIcon={app}\novadoom.exe
EnableDirDoesntExistWarning=true
DirExistsWarning=no
AllowRootDirectory=True
ChangesAssociations=yes
ArchitecturesInstallIn64BitMode=x64
WizardImageFile={#SourcePath}\media\wininstall_largeback.bmp
WizardSmallImageFile={#SourcePath}\media\wininstall_wizardicon.bmp

[Languages]
Name: english; MessagesFile: compiler:Default.isl
Name: french; MessagesFile: compiler:Languages\French.isl
Name: german; MessagesFile: compiler:Languages\German.isl
Name: spanish; MessagesFile: compiler:Languages\Spanish.isl
Name: en; MessagesFile: compiler:Default.isl
Name: fr; MessagesFile: compiler:Languages\French.isl
Name: de; MessagesFile: compiler:Languages\German.isl
Name: es; MessagesFile: compiler:Languages\Spanish.isl

[Types]
Name: full; Description: Full installation
Name: compact; Description: Client-only installation
Name: custom; Description: Custom installation; Flags: iscustom

[Components]
Name: base; Description: Base data; Types: full compact custom; Flags: fixed
Name: client; Description: NovaDoom Client; Types: full compact custom; Flags: DisableNoUninstallWarning
Name: server; Description: NovaDoom Server; Types: full; Flags: DisableNoUninstallWarning
Name: launcher; Description: NovaLaunch (Game Launcher); Types: full compact custom; Flags: DisableNoUninstallWarning

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; COMMON FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Source: {#SourcePath}\OutCommon\*.txt; DestDir: {app}; Flags: ignoreversion; Components: base
Source: {#SourcePath}\OutCommon\config-samples\*; DestDir: {app}\config-samples; Flags: ignoreversion; Components: server
Source: {#SourcePath}\OutCommon\licenses\*; DestDir: {app}\licenses; Flags: ignoreversion; Components: base
Source: {#SourcePath}\OutCommon\novadoom.wad; DestDir: {app}; Flags: ignoreversion; Components: client server

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 64-BIT FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Source: {#SourcePath}\OutX64\libwavpack-1.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\libgme.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\libxmp.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\libogg-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\libopus-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\libopusfile-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\novalaunch.exe; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\novadoom.exe; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\novasrv.exe; DestDir: {app}; Flags: ignoreversion; Components: server; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\SDL2_mixer.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\SDL2.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxbase315u_net_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxbase315u_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxbase315u_xml_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxmsw315u_core_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxmsw315u_html_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\wxmsw315u_xrc_vc14x_x64.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: Is64BitInstallMode
Source: {#SourcePath}\OutX64\redist\VC_redist.x64.exe; DestDir: {tmp}; Flags: dontcopy

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 32-BIT FILES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Source: {#SourcePath}\OutX86\libwavpack-1.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\libgme.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\libxmp.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\libogg-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\libopus-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\libopusfile-0.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\novalaunch.exe; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\novadoom.exe; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\novasrv.exe; DestDir: {app}; Flags: ignoreversion; Components: server; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\SDL2_mixer.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\SDL2.dll; DestDir: {app}; Flags: ignoreversion; Components: client; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxbase315u_net_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxbase315u_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxbase315u_xml_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxmsw315u_core_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxmsw315u_html_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\wxmsw315u_xrc_vc14x.dll; DestDir: {app}; Flags: ignoreversion; Components: launcher; Check: not Is64BitInstallMode
Source: {#SourcePath}\OutX86\redist\VC_redist.x86.exe; DestDir: {tmp}; Flags: dontcopy

[InstallDelete]

Type: files; Name: "{app}\libFLAC-8.dll"
Type: files; Name: "{app}\libmpg123-0.dll"
Type: files; Name: "{app}\libvorbis-0.dll"
Type: files; Name: "{app}\libvorbisfile-3.dll"
Type: files; Name: "{app}\libmodplug-1.dll"

[Icons]
Name: {group}\NovaDoom Client; Filename: {app}\novadoom.exe; WorkingDir: {app}
Name: {group}\NovaDoom Launcher; Filename: {app}\novalaunch.exe; WorkingDir: {app}
Name: {group}\NovaDoom Server; Filename: {app}\novasrv.exe; WorkingDir: {app}
Name: {group}\NovaDoom User Folder; Filename: "%USERPROFILE%\Documents\My Games\NovaDoom"
Name: {group}\{cm:UninstallProgram,NovaDoom}; Filename: {uninstallexe}

[Code]
function GetUninstallString: string;
var
  sUnInstPath: string;
  sUnInstallString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#SetupSetting("AppID")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;


function GetRegistryVersion: string;
var
  sUnInstPath: string;
  sVersionString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#SetupSetting("AppID")}_is1');
  sVersionString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'DisplayVersion', sVersionString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'DisplayVersion', sVersionString);
  Result := sVersionString;
end;


function IsUpgrade: Boolean;
begin
  Result := (GetUninstallString() <> '');
end;


function Count(What, Where: String): Integer;
begin
   Result := 0;
    if Length(What) = 0 then
        exit;
    while Pos(What,Where)>0 do
    begin
        Where := Copy(Where,Pos(What,Where)+Length(What),Length(Where));
        Result := Result + 1;
    end;
end;


//split text to array
procedure Explode(var ADest: TArrayOfString; aText, aSeparator: String);
var tmp: Integer;
begin
    if aSeparator='' then
        exit;

    SetArrayLength(ADest,Count(aSeparator,aText)+1)

    tmp := 0;
    repeat
        if Pos(aSeparator,aText)>0 then
        begin

            ADest[tmp] := Copy(aText,1,Pos(aSeparator,aText)-1);
            aText := Copy(aText,Pos(aSeparator,aText)+Length(aSeparator),Length(aText));
            tmp := tmp + 1;

        end else
        begin

             ADest[tmp] := aText;
             aText := '';

        end;
    until Length(aText)=0;
end;


//compares two version numbers, returns -1 if vA is newer, 0 if both are identical, 1 if vB is newer
function CompareVersion(vA,vB: String): Integer;
var tmp: TArrayOfString;
    verA,verB: Array of Integer;
    i,len: Integer;
begin

    StringChange(vA,'-','.');
    StringChange(vB,'-','.');

    Explode(tmp,vA,'.');
    SetArrayLength(verA,GetArrayLength(tmp));
    for i := 0 to GetArrayLength(tmp) - 1 do
        verA[i] := StrToIntDef(tmp[i],0);

    Explode(tmp,vB,'.');
    SetArrayLength(verB,GetArrayLength(tmp));
    for i := 0 to GetArrayLength(tmp) - 1 do
        verB[i] := StrToIntDef(tmp[i],0);

    len := GetArrayLength(verA);
    if GetArrayLength(verB) < len then
        len := GetArrayLength(verB);

    for i := 0 to len - 1 do
        if verA[i] < verB[i] then
        begin
            Result := 1;
            exit;
        end else
        if verA[i] > verB[i] then
        begin
            Result := -1;
            exit
        end;

    if GetArrayLength(verA) < GetArrayLength(verB) then
    begin
        Result := 1;
        exit;
    end else
    if GetArrayLength(verA) > GetArrayLength(verB) then
    begin
        Result := -1;
        exit;
    end;

    Result := 0;
end;


function InitializeSetup(): Boolean;
var
  V: Integer;
  iUpgradeResult: Integer;
  iResultCode: Integer;
  iVersionCompare: Integer;
  sUnInstallString: string;
  sVersion: string;
  sOldVersion: string;
  bUpgrade: Boolean;
  bSilent: Boolean;
begin
  bSilent := WizardSilent();
  if bSilent = False then
  begin
    bUpgrade := IsUpgrade();
    if bUpgrade then
    begin
      sVersion := '{#SetupSetting("AppVersion")}';
      sOldVersion := GetRegistryVersion();
      iVersionCompare := CompareVersion(sOldVersion, sVersion);
      if iVersionCompare = -1 then
          iUpgradeResult := MsgBox('The version of NovaDoom you are about to install is a ' + \
          'downgrade to the currently installed version. Do you want to proceed with the installation?'
          , mbConfirmation, MB_YESNO);
      if iVersionCompare = 0 then
          iUpgradeResult := MsgBox('The version of NovaDoom you are about to install is identical ' + \
          'to the currently installed version. Do you want to proceed with the installation?',
          mbConfirmation, MB_YESNO);
      if iVersionCompare = 1 then
          iUpgradeResult := IDYES;

      if iUpgradeResult = IDYES then
      begin
        V := MsgBox('NovaDoom has been detected on this machine. ' + \
        'Would you like to do an in-place upgrade?' #13#10 #13#10 + \
        'If you want to upgrade your copy of NovaDoom in-place, press "Yes." This is the suggested choice.' #13#10 + \
        'If you want to do a fresh installation of NovaDoom, in order to change your installation directory ' + \
        'or installation type, press "No."' #13#10 + \
        'If you want to exit the installation, press "Cancel."',
        mbConfirmation, MB_YESNOCANCEL);

        if V = IDYES then
        begin
          Result := True;
          exit;
        end;
        if V = IDNO then
        begin
          sUnInstallString := GetUninstallString();
          sUnInstallString := RemoveQuotes(sUnInstallString);
          Exec(ExpandConstant(sUnInstallString), '', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
          Result := False;
          exit;
        end;
        if V = IDCANCEL then
        begin
          Result := False;
          exit;
        end;
      end
      else
      begin
        Result := False;
        exit;
      end;
    end;
  end;

  Result := True;
end;


function VC2017RedistNeedsInstall(Platform: String): Boolean;
var
  Version: String;
  KeyLocation: String;
begin
  KeyLocation := 'SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\' + Platform;
  if RegQueryStringValue(HKEY_LOCAL_MACHINE,
       KeyLocation, 'Version',
       Version) then
  begin
    // Is the installed version at least 14.40?
    Log('VC Redist Version check : found ' + Version);
    Result := (CompareVersion(Version, 'v14.40.33810.0')>0);
  end
  else
  begin
    // Not even an old version installed
    Result := True;
  end;
  if (Result) then
  begin
    ExtractTemporaryFile('VC_redist.' + Platform + '.exe');
  end;
end;

[Run]
Filename: "{tmp}\VC_redist.x64.exe"; \
  StatusMsg: "Installing VC++ Runtime"; \
  Parameters: "/install /passive /norestart"; \
  Check: VC2017RedistNeedsInstall('x64') and Is64BitInstallMode; \
  Flags: waituntilterminated

Filename: "{tmp}\VC_redist.x86.exe"; \
  StatusMsg: "Installing VC++ Runtime"; \
  Parameters: "/install /passive /norestart"; \
  Check: VC2017RedistNeedsInstall('x86') and (not Is64BitInstallMode); \
  Flags: waituntilterminated

Filename: {app}\novalaunch.exe; Description: {cm:LaunchProgram,NovaLaunch}; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: {app}\novadoom.out
Type: files; Name: {app}\novadoom.cfg
Type: files; Name: {app}\novasrv.cfg
Type: files; Name: {app}\*.log
Type: dirifempty; Name: {app}

[Registry]
; .odd file association
#define NovaDoomDemoExt ".odd"
#define NovaDoomDemoFile NovaDoomName + "File" + NovaDoomDemoExt

Root: HKA; Subkey: {#"Software\Classes\" + NovaDoomDemoExt +  "\OpenWithProgids"}; ValueType: string; ValueName: {#NovaDoomDemoFile}; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: {#"Software\Classes\" + NovaDoomDemoFile}; ValueType: string; ValueName: ""; ValueData: {#NovaDoomName + " Demo"}; Flags: uninsdeletekey
Root: HKA; Subkey: {#"Software\Classes\" + NovaDoomDemoFile + "\DefaultIcon"}; ValueType: string; ValueName: ""; ValueData: "{app}\novadoom.exe,1"
Root: HKA; Subkey: {#"Software\Classes\" + NovaDoomDemoFile + "\shell\open\command"}; ValueType: string; ValueName: ""; ValueData: """{app}\novadoom.exe"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\novadoom.exe\SupportedTypes"; ValueType: string; ValueName: {#NovaDoomDemoExt}; ValueData: ""

; novadoom:// URI scheme
Root: HKA; Subkey: "Software\Classes\novadoom"; ValueType: string; ValueName: ""; ValueData: "URL:NovaDoom Protocol"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\novadoom"; ValueType: string; ValueName: "URL Protocol"; ValueData: ""; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\novadoom\Shell\Open\Command"; ValueType: string; ValueName: ""; ValueData: """{app}\novadoom.exe"" ""%1"""; Flags: uninsdeletekey
